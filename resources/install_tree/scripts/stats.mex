//////////////////////////////////////////////////////////////////////////////
//
// File: stats.mex
//
// Desc: Statistics display
//
// Copyright 1990, 1994 by Lanius Corporation.  All rights reserved.
//
// $Id: stats.mex,v 1.1 2002/10/01 17:50:41 sdudley Exp $
//
//////////////////////////////////////////////////////////////////////////////

#include <max.mh>
#include <intpad.mh>
#include <longpad.mh>

string lang_get(string: key);

int main(string: argv)
{
    struct _stamp: now;
    string: minutes,
            files,
            kb,
            cents,
            lpc,
            newline,
            comma;

    id.instant_video:=0;
    timestamp(now);

    newline:=lang_get("global.stat_newline");
    comma:=lang_get("global.stat_comma");

    print(lang_get("global.stat_hdr"), stamp_string(now), newline, '\n');

    minutes:=lang_get("global.stat_minutes");
    files:=lang_get("global.stat_file");
    kb:=lang_get("global.stat_kb");
    cents:=lang_get("global.stat_cents");
    lpc:=stamp_string(usr.date_pwd_chg);

    print(lang_get("global.stat_time"));
    print(lang_get("global.stat_time_online"), ulongpadleft(timeon(), 6, ' '),     minutes, newline);
    print(lang_get("global.stat_time_remain"), ulongpadleft(timeleft(), 6, ' '),   minutes, newline);
    print(lang_get("global.stat_time_prev"),   uintpadleft(usr.time, 6, ' '),     minutes, newline);
    print(lang_get("global.stat_time_calls"),  uintpadleft(usr.times, 6, ' '),    newline);
    print(lang_get("global.stat_files"));
    print(lang_get("global.stat_file_ul"),     ulongpadleft(usr.up, 6, ' '),       kb, comma, ulongpadleft(usr.nup, 5, ' '),       files, newline);
    print(lang_get("global.stat_file_dl"),     ulongpadleft(usr.down, 6, ' '),     kb, comma, ulongpadleft(usr.ndown, 5, ' '),     files, newline);
    print(lang_get("global.stat_file_dlday"),  ulongpadleft(usr.downtoday, 6, ' '),kb, comma, ulongpadleft(usr.ndowntoday, 5, ' '),files, newline);
    print(lang_get("global.stat_file_dlavl"),  uintpadleft(class_info(usr.priv, CIT_DL_LIMIT) - usr.downtoday, 6, ' '), kb, newline);

    if (usr.credit or usr.debit)
    {
        print(lang_get("global.stat_matrix"));
        print(lang_get("global.stat_net_credit"), uintpadleft(usr.credit, 6, ' '), cents, newline);
        print(lang_get("global.stat_net_debit"),  uintpadleft(usr.debit, 6, ' '),  cents, newline);
    }

    if (usr.expdate or usr.expmins)
    {
        print(lang_get("global.stat_subscrip"));

        if (usr.expmins)
            print(lang_get("global.stat_sub_mins"), usr.xp_mins - timeon(), minutes, newline);

        if (usr.expdate)
            print(lang_get("global.stat_sub_date"), stamp_string(usr.xp_date), newline);
    }

    print(lang_get("global.stat_misc"));
    print(lang_get("global.stat_pwd_chg"), lpc, newline, '\n');

    return 0;
}

